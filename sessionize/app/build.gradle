apply plugin: 'com.android.application'
apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlin-android-extensions'

def firebaseEnabled = file("./google-services.json").exists()
def releaseEnabled = file("./release.jks").exists()

if (firebaseEnabled) {
    apply plugin: 'io.fabric'
}

Properties properties = new Properties()
try {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}
catch(Exception e) {

}

def releasePassword = properties.getProperty('releasePassword', '')

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "co.touchlab.droidcon.lisbon2019"
        buildConfigField "String", "TIME_ZONE", "\"Europe/Lisbon\""
        buildConfigField "boolean", "FIREBASE_ENABLED", "${firebaseEnabled}"

        minSdkVersion 24
        targetSdkVersion 28
        versionCode 10003
        versionName "1.0.3"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }
    if(releaseEnabled) {
        signingConfigs {
            release {
                storeFile file("release.jks")
                keyAlias "key0"
                storePassword "$releasePassword"
                keyPassword "$releasePassword"
            }
        }
    }

    buildTypes {
        if(releaseEnabled) {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                signingConfig signingConfigs.release
            }
        }

        //This is for MultiplatformSettings
        debug {
            // MPP libraries don't currently get this resolution automatically
            matchingFallbacks = ['release']
        }
    }

    lintOptions {
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/*.kotlin_module'
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

kotlin {
    sourceSets {
        commonMain {
            dependencies {
                implementation project(':sessionize:lib')
            }
        }
    }

    targets {
        fromPreset(presets.android, 'androidApp')
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"

    implementation 'com.nex3z:flow-layout:1.2.2'
    implementation "androidx.lifecycle:lifecycle-extensions:2.0.0"
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:2.0.0"

    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.vectordrawable:vectordrawable:1.0.1'

    implementation 'de.hdodenhof:circleimageview:2.1.0'
    implementation 'com.squareup.picasso:picasso:2.71828'

    implementation deps.sqldelight.runtimeJdk
    implementation deps.sqldelight.driverAndroid
    implementation deps.kotlin.coroutines.jdk
    implementation deps.kotlin.coroutines.android
//    implementation deps.ktor.androidCore
    implementation deps.firebase.androidCore
    implementation deps.firebase.androidMessaging
    implementation deps.crashlytics

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    implementation deps.multiplatformSettings

    implementation "androidx.navigation:navigation-fragment-ktx:2.1.0-alpha06"
    implementation "androidx.navigation:navigation-ui-ktx:2.1.0-alpha06"
    implementation deps.firebase.firestoreAndroid
}

apply plugin: "androidx.navigation.safeargs"


if (firebaseEnabled) {
    apply plugin: 'com.google.gms.google-services'
}
